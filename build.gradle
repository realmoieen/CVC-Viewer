plugins {
    id 'java'
    id 'application'
    id("com.gradleup.shadow") version "9.3.1"
    id 'edu.sc.seis.launch4j' version '4.0.0'
}

group = 'com.moieen.cvc.gui'
version = '2.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

application {
    mainClass = 'com.moieen.cvc.gui.cvcviewer.Lunch'
}

repositories {
    mavenCentral()
}

dependencies {
    // System-scope dependencies (local JARs)
    implementation files('lib/testbedutils-CVC-1.3.jar')
    implementation files('lib/bcprov-jdk18on-1.78.1.jar')

    // Regular dependency
    implementation 'org.jdesktop:beansbinding:1.2.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    // JUnit 5 API + Engine
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

    // REQUIRED: JUnit Platform Launcher (fixes your error)
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.2'

}
tasks.named("test") {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
jar {
    archiveBaseName = project.name
    manifest {
        attributes(
                'Main-Class': 'com.moieen.cvc.gui.cvcviewer.Lunch'
        )
    }
}
shadowJar {

}
launch4j {
    mainClassName = 'com.moieen.cvc.gui.cvcviewer.Lunch'
    icon = "${project.rootDir}/src/main/resources/certificate16x16.ico"
    classpath = []
    jarTask = project.tasks.shadowJar
    errTitle = "Error CVC-Viewer"
    version = project.version
    textVersion = "{$project.version}"
    description = "The Utility to view CV Certificate.Card Verifiable Certificates (CVC) are digital certificates that are designed to be processed by devices."
    copyright = "Copyright (c) 2026 - Moieen Abbas"
    companyName = "Moieen Abbas (realmoieen@gmail.com)(+923012605119)"
    jreMinVersion = '1.8.0_131'
    supportUrl = "https://github.com/realmoieen/CVC-Viewer/issues"
}

// Encoding configuration
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.register('windowsZip', Zip) {
    group = 'distribution'
    description = 'Packages Launch4j EXE into a Windows ZIP'

    // Ensure EXE is created first
    dependsOn createExe

    // ZIP name: *-windows.zip
    archiveBaseName = "${project.name}"
    archiveClassifier = 'windows'
    archiveExtension = 'zip'

    destinationDirectory = layout.buildDirectory.dir('distributions')

    // Include EXE
    from(layout.buildDirectory.dir('launch4j')) {
        include "*.exe"
    }

    // Optional: include README / license
    from(projectDir) {
        include 'CVC_Registry.reg'
        include 'README.md'
        include 'LICENSE'
    }
}
